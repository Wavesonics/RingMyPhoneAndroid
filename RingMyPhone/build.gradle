apply plugin: 'com.android.application'

repositories {
    google()
    mavenCentral()
}

android {
    compileSdkVersion 31
    buildToolsVersion '32.0.0'

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        minSdkVersion 14
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 25
        versionCode 13
        versionName '2.6'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {

        }
    }

    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    // Read our private signing details from the local properties file
    if (project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyAlias') &&
            project.hasProperty('keyPassword')) {
        println 'gradle.properties OK'
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        println 'gradle.properties not well formed'
        if (!project.hasProperty('storeFile')) println 'storeFile not defined'
        if (!project.hasProperty('storePassword')) println 'storePassword not defined'
        if (!project.hasProperty('keyAlias')) println 'keyAlias not defined'
        if (!project.hasProperty('keyPassword')) println 'keyPassword not defined'

        buildTypes.release.signingConfig = null
    }
}

dependencies {
    // Support Libraries
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'com.google.guava:guava:31.0.1-android'
    implementation 'com.getpebble:pebblekit:3.0.0@aar'
    // TODO(Noah): Switch to snackbar
    implementation('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') {
        exclude group: 'com.google.android', module: 'support-v4'
    }
}
