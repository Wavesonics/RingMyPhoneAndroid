buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/groups/public/" }
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 13
        versionName '2.6'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {

        }
    }

    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    // Read our private signing details from the local properties file
    if (project.hasProperty('storeFile') &&
            project.hasProperty('storePassword') &&
            project.hasProperty('keyAlias') &&
            project.hasProperty('keyPassword')) {
        println 'gradle.properties OK'
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyAlias = keyAlias
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        println 'gradle.properties not well formed'
        if (!project.hasProperty('storeFile')) println 'storeFile not defined'
        if (!project.hasProperty('storePassword')) println 'storePassword not defined'
        if (!project.hasProperty('keyAlias')) println 'keyAlias not defined'
        if (!project.hasProperty('keyPassword')) println 'keyPassword not defined'

        buildTypes.release.signingConfig = null
    }
    dexOptions {
        incremental true
    }
}

dependencies {
    // Support Libraries
    compile 'com.android.support:support-annotations:22.0.0'
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:support-v13:22.0.0'
    compile 'com.google.android.gms:play-services:7.0.0'
    compile 'com.google.guava:guava:18.0'
    compile 'com.jakewharton:butterknife:6.0.0'
    compile 'com.getpebble:pebblekit:3.0.0@aar'
    compile('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') {
        exclude group: 'com.google.android', module: 'support-v4'
    }
}
